//---------------------------------------------------------------------------------------//
// GetParams (  )
// by Alexander Zueiv
// October 1, 2006
//
// This function defines local variable ($) for every parameter specified in the current script's name.
// To refer to any parameter within the script you will only need to specify it's name with leading $.
// It also validates every "required" parameter for "not IsEmpty()" and returns "1" if all succeeded.
//
// This function does not parse script parameters itself. It is a "wrapper" for your own custom function;
// it's supposed to call the custom function that you do currently use to get a parameter's value by it's
// name. This should work well whatever method of sending parameters do you currently use. FMP will
// show you where to put your function call when you click OK to save this function.
//
// Script name format:
//
//    Cool Scrip [ paramA ; ( paramB | paramC ) {; paramD} ]
//
// Script code sample:
//
//    If [ GetParams ]
//       Set Field [ Field1 ; $paramA ]
//       Set Field [ Field2 ; If ( IsEmpty ( $paramB ) ; $paramC ; $paramB ) ]
//       If [ not IsEmpty ( $paramD ) ]
//          Set Field [ Field3 ; $paramD ]
//       End If
//    Else
//       Show Custom Dialog [ Get ( ScriptName ) ; "Error! Some script parameter is missing." ]
//    End If
//
// This script will work only if ( paramA and ( paramB or paramC ) ) aren't empty, otherwise it will show
// the error message.
//
// To specify required validation logic for "alternative" parameters they must be enclosed in parentheses.
// The "optional" parameters section {} must appear at the end.
//
//---------------------------------------------------------------------------------------//

Let ( [

// adjust these variables to match your scripts naming style
BEGIN = "[" ;    // beginning of parameters definition
BREAK = ";" ;    // regular parameters separator (and)
ALT = "|" ;     // alternative parameters separator (or)
OPTION = "{" ;    // beginning of optional parameters section
END = "]" ;    // end of parameters definition

SCRIPT = Get ( ScriptName ) ;
PARAMS = Middle ( SCRIPT ;
   Position ( SCRIPT ; BEGIN ; 1 ; 1 ) +1 ;
   Position ( SCRIPT ; END ; Length ( SCRIPT ) ; -1 ) - Position ( SCRIPT ; BEGIN ; 1 ; 1 ) -1 );
PARAMS_JOINED = Substitute(PARAMS; "_"; "xzx") // make sure underline don't break the wordcount method


] ;

Case (

IsEmpty(PARAMS); 1;

//---------------------------------------------------------------------------------------//

$_SCRIPT_PARAMETERS_COUNTER >= 1 ;

Let ( [

NAME = Substitute(MiddleWords ( PARAMS_JOINED ; $_SCRIPT_PARAMETERS_COUNTER ; 1 ); "xzx"; "_") ;
$_SCRIPT_PARAMETERS_COUNTER = $_SCRIPT_PARAMETERS_COUNTER - 1 ;

// put your function call for getting script parameters here
VALUE = json.#Get ( Get(ScriptParameter) ;  NAME )

] ;

Evaluate ( "Let($" & NAME & "=" & Quote ( VALUE ) & ";\"\")" ) &

If ( $_SCRIPT_PARAMETERS_COUNTER >= 1 ; json.#Assign ) // recursive call to this function

) ; // End Let

//---------------------------------------------------------------------------------------//

WordCount ( PARAMS ) >= 1 ;

Let ( [

$_SCRIPT_PARAMETERS_COUNTER = WordCount(  PARAMS_JOINED) ;
TEMP = json.#Assign ; // recursive call to this function
TEMP = Substitute ( PARAMS ; [ " " ; "" ] ; [ "$" ; "" ] ) ;
TEMP = If ( Position ( TEMP ; OPTION ; 1 ; 1 ) ; Left ( TEMP ; Position ( TEMP ; OPTION ; 1 ; 1 ) -1 ) ; TEMP ) ;
TEMP = If ( Left ( TEMP ; 1 ) = "(" ; "(not IsEmpty($" & Right ( TEMP ; Length ( TEMP ) -1 ) ; "not IsEmpty($" & TEMP ) ;
VALIDATION = Substitute ( TEMP ;
   [ BREAK & "(" ; ") and (not IsEmpty($" ] ;
   [ BREAK ; ") and not IsEmpty($" ] ;
   [ ALT ; ") or not IsEmpty($" ] ) & ")" ;
RESULT = If ( VALIDATION = "not IsEmpty($)" ; 1 ; Evaluate ( VALIDATION ) )

] ;

RESULT

// If you use any Script Trigger or Dialog plug-in you could show the error message right here:
// If ( RESULT <> 1 ; ...

) // End Let

//---------------------------------------------------------------------------------------//

) // End Case

) // End Let

