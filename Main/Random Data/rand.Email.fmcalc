// rand.Email ( _first; _last )
//
// Purpose:		Returns a fake email address.
//
// Parameters:		_first:	first name of the persom for whom the email address will be generated, can
//						blank
//				_last:		last name of the persom for whom the email address will be generated, can
//						blank
//
// Requirements:	rand.Words
//				rand.Integer
//				data.Alphabet
//				rand.CompanyName
//				devp.Nil
//				rand.ListItem
//
// Author:			Mislav Kos <mkos@soliantconsulting.com>
// Version:		1.0 written 14-10-15
//				1.1 edited 15-05-22 by Charles Ross for readability and use of sub functions
//				1.2 edited 15-05-23 by Charles Ross for more variation in user name and domains
//
// Notes:			The fake email ALWAYS includes the word "test" right before the @ sign
//
// Example:
// // A possible exampleâ€¦
// rand.Email ( "Charles"; "Ross" ) = "crosstest@molestieclass.org"

Let (
  [
    _first = Case (
      IsEmpty ( _first );		rand.Words ( 1; 1 );
					_first
    );

    _first = Choose (
      rand.Integer ( 0; 1 );	Left ( _first; 1 );
					_first
    );

    _last = Case (
      IsEmpty ( _last );		rand.Words ( 1; 1 );
					_last
    );

    _sep = Choose (
      rand.Integer ( 0; 2 );
     ".";	"_";	devp.Nil
    );

    _name	= _first & _sep & _last;
    _prefix	= Filter ( Lower ( _name ) ; data.Alphabet & "._" );
    _at	= "test@";

    _tlds = List ( ".com"; ".net"; ".org"; ".info"; ".biz"; ".edu" );
    _company_domain = Substitute (
      Lower ( rand.CompanyName );
      [	" ";	devp.Nil ];
      [ ".";	devp.Nil ]
    );

    _domain = Choose (
      rand.Integer ( 0; 1 );
      rand.ListItem ( data.EmailProvidersList );
      _company_domain & rand.ListItem ( _tlds )
    );

    _result = _prefix & _sep & _at & _domain
  ];

  _result
)
