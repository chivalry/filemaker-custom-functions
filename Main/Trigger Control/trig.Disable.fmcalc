// trig.Disable
//
// Purpose:		Sets global variables to indicate that the current script is suppressing triggers.
//
// Requirements: 	$$_TRIGGERS_DISABLED
//				$$_TRIGGERS_DISABLED_SCRIPTS
//				$_triggers_disabled_key
//
// Author:			Jeremy Bante
// Version:		1.0 written 13-04-08
//
// Notes:			https://github.com/jbante/FileMaker-Techniques/blob/master/CustomFunctions/Triggers/TriggersAreActive.fmfn
//				http://filemakerstandards.org/display/bp/Suppressible+Triggered+Scripts
//
//				This function must be called from within a script. Otherwise it will not supress
//				triggers and will return False.
//
// Example:
// // When called within a scriptâ€¦
// trig.Disable = True

Let (
  [
    _script_name		= Get ( ScriptName ) ;
    _script_is_running		= not IsEmpty ( _script_name ) ;

    // Has this script already disabled triggers?
    _disable_is_running	= not IsEmpty ( $_triggers_disabled_key ) ;

    // If this script already disabled triggers, use the existing key, otherwise generate a new one.
    $_triggers_disabled_key = Case (
      _disable_is_running	; $_triggers_disabled_key ;
      _script_is_running	; Get ( UUID )
    ) ;

    // If this is the first time this script is disabling triggers, add it to the list.
    $$_TRIGGERS_DISABLED_SCRIPTS = Case (
      _script_is_running and not _disable_is_running ;		_script_name & trig.ScriptKey ;
      											devp.Nil
    ) & $$_TRIGGERS_DISABLED_SCRIPTS;

    $$_TRIGGERS_DISABLED = _script_is_running or $$_TRIGGERS_DISABLED
  ] ;

  _script_is_running
)
