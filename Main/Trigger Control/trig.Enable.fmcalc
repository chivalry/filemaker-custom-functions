// trig.Enable
//
// Purpose:		Sets global variables to indicate that the current script is no longer suppressing
//				triggers.
//
// Requirements: 	$$_TRIGGERS_DISABLED
//				$$_TRIGGERS_DISABLED_SCRIPTS
//				$_triggers_disabled_key
//
// Author:			Jeremy Bante
// Version:		1.0 written 13-04-08
//
// Notes:			https://github.com/jbante/FileMaker-Techniques/blob/master/CustomFunctions/Triggers/TriggersAreActive.fmfn
//				http://filemakerstandards.org/display/bp/Suppressible+Triggered+Scripts
//
//				This function checks to see if there are other scripts that are suppressing triggers
//				before enabling them, preventing subscripts for enabling triggers that calling
//				scripts have disabled.
//
// Example:
// // When called within a script but a calling script has already disabled triggers…
// trig.Enable = False

If ( not IsEmpty ( $_triggers_disabled_key ) ;
	Let ( [
		_scripts_len = Length ( $$_TRIGGERS_DISABLED_SCRIPTS ) ;
		_script_pos =
			Position (
				$$_TRIGGERS_DISABLED_SCRIPTS ;
				Char ( 9 ) /* tab */ & $_triggers_disabled_key & ¶ ;
				_scripts_len ;
				-1
			) ;
		_break_pos =
			If ( _script_pos > 0 ;
				Position ( $$_TRIGGERS_DISABLED_SCRIPTS ; ¶ ; _script_pos ; 1 ) ;
				/* Else */ 0
			) ;
		$$_TRIGGERS_DISABLED_SCRIPTS =
			Right ( $$_TRIGGERS_DISABLED_SCRIPTS ; _scripts_len - _break_pos ) ;
		$$_TRIGGERS_DISABLED =
			If ( not IsEmpty ( $$_TRIGGERS_DISABLED_SCRIPTS ) and $$_TRIGGERS_DISABLED ;
				True ;
				/* Else */ ""
			) ;
		$_triggers_disabled_key = ""
	] ;
		""
	)
)
