// plug.Attribute ( _name; _position )
//
// Purpose:		Returns the plugin attribute at the given position.
//
// Parameters:		_name:	The name of the plugin to get the attribute of
//				_position:	Which position the desired attribute is in.
//
// Author:			Charles Ross
// Version:		1.0 written 15-04-19
//
// Todo:			See if there's a way to make this function compatible with 11-.
//
// Example:
// // Assuming ScriptMaster is installed and at version 4.32...
// plug.Attribute ( "360Works ScriptMaster"; 2 ) = 4.32

Let(
  [
    _plugins = Get ( InstalledFMPlugins );

    _installed = PatternCount (
      _plugins;
      _name
    );

    _plugin_pos		= Position ( _plugins; _name; 1; 1 );
    _before_name	= Left ( _plugins; _plugin_pos );
    _pos_in_list		= PatternCount ( _before_name; "¶" ) + 1;
    _line			= GetValue ( _plugins; _pos_in_list );
    _list			= Substitute ( _line; ";"; "¶" );

    _result = GetValue ( _list; _position )
  ];

  _result
)
