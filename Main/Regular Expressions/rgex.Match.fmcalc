// rgex.Match ( _string; _pattern )
//
// Purpose:		Returns true if the pattern is contained in the string.
//
// Parameters:		_string:	The string to search for the pattern in.
//				_pattern:	The pattern to search for.
//
// Requirements: 	The ScriptMaster plugin, written with version 4.32.
//
// Author:			Charles Ross
// Version:		1.0 written 15-04-16
//
// Notes:			
//
// Todo:			
//
// Example:
// ( rgex.Match ( "foo"; "foo" ) = 1 )
//  and
// ( rgex.Match ( "bar"; "foo" ) = 0 )

Case (
  plug.PluginIsReady ( "ScriptMaster" );
  Let (
    [
      _signature = "rgex_Match( string ; pattern )";

      // Register the function if it hasn't been already.
      _ = Case (
        not PatternCount ( SMGetRegisteredModules ; _signature );
        Let (
          [
            _code =
              "import java.util.regex.*;¶" &
              "¶" &
              "return Pattern.matches( pattern, string );"
          ];

          RegisterGroovy ( _signature; _code )
        )
      );

      _calc = "rgex_Match ( " &
        Quote ( _string ) & "; " &
        Quote ( _pattern ) & " )"
    ];

    Evaluate ( _calc )
  )
)
