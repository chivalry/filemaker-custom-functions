// nump.FormatOrdinal ( _number )
//
// Purpose:		Returns the number as ordinal text ("1st", "3rd", etc.) with the ordinal text superscripted.
//
// Parameters:		_number:	The number to return as an ordinal.
//
// Author:			Vaughan Bromfield, Bob Weaver, LaRetta
// Version:		1.2 edited 15-03-26 â€” make superscript a constant instead of string
//
// Notes:			The superscript feature was added by an unknown individual.
//
//				Adapted from http://www.briandunning.com/cf/737
//
// Example:
// nump.FormatOrdinal ( 4 ) = "4th" // with "th" as a superscript.

Let (
  [ 
    _style	= Superscript; // Can be made null to remove formatting
    _digit	= Int( GetAsNumber( _number ) );
    _tens	= Mod ( Abs( _digit ); 100 );

    _ordinal = Choose (
      Min ( 4 ; Mod ( _tens ; 10 ) ) * ( ( _tens < 11 ) or ( _tens > 13 ) );
      "th"; "st"; "nd"; "rd"; "th"
    )
  ];

  _digit & TextStyleAdd ( _ordinal; _style )

)
